module okada_device

use VariableDefination
    
contains

attributes(device) subroutine okada1985(x,y,u1,u2,u3,depth,xlength,xwidth,slip,strike,dip,rake,x0,y0)

implicit NONE
real(fp_kind)   ::  x, y, u1, u2, u3, depth, xlength, xwidth, slip, strike, dip, rake, x0, y0
real(fp_kind)   ::  alp, disl1, disl2, disl3
real(fp_kind)   ::  cd, sd, cs, ss, xr, yr
real(fp_kind)   ::  u11, u12, u21, u22, u31, u32, tmp1, tmp2
real(fp_kind), parameter   ::  PI = 3.141593

alp = 0.667; disl3 = 0.0

disl1 = slip*COS(rake*PI/180.0)
disl2 = slip*SIN(rake*PI/180.0)
cd = COS(dip*PI/180.0)
sd = SIN(dip*PI/180.0)
cs = COS(strike*PI/180.0)
ss = SIN(strike*PI/180.0)
xr = (x-x0)*ss + (y-y0)*cs
yr = (y-y0)*ss - (x-x0)*cs

call srectf(alp,xr,yr,depth,-xlength/2.0,xlength/2.0, &
            -xwidth/2.0,xwidth/2.0,sd,cd,disl1,disl2,disl3, &
            u1,u2,u3,u11,u12,u21,u22,u31,u32)

tmp1 = u1*ss - u2*cs
tmp2 = u1*cs + u2*ss
      
u1 = tmp1
u2 = tmp2

end subroutine okada1985




attributes(device) subroutine okada1985_real8(x,y,u1,u2,u3,depth,xlength,xwidth,slip,strike,dip,rake,x0,y0)

implicit NONE
real(fp_kind)   ::  x, y, u1, u2, u3, depth, xlength, xwidth, slip, strike, dip, rake, x0, y0
real*8  ::  x_d, y_d, u1_d, u2_d, u3_d, depth_d, xlength_d, xwidth_d, slip_d, strike_d, dip_d, rake_d, x0_d, y0_d
real*8  ::  alp, disl1, disl2, disl3
real*8  ::  cd, sd, cs, ss, xr, yr
real*8  ::  u11, u12, u21, u22, u31, u32, tmp1, tmp2
real*8, parameter   ::  PI = 3.141593

x_d = DBLE(x); y_d = DBLE(y)
depth_d = DBLE(depth); xlength_d = DBLE(xlength); xwidth_d = DBLE(xwidth)
slip_d = DBLE(slip); strike_d = DBLE(strike); dip_d = DBLE(dip)
rake_d = DBLE(rake); x0_d = DBLE(x0); y0_d = DBLE(y0)

alp = 0.667; disl3 = 0.0
disl1 = slip_d*COS(rake_d*PI/180.0)
disl2 = slip_d*SIN(rake_d*PI/180.0)
cd = COS(dip_d*PI/180.0)
sd = SIN(dip_d*PI/180.0)
cs = COS(strike_d*PI/180.0)
ss = SIN(strike_d*PI/180.0)
xr = (x_d-x0_d)*ss + (y_d-y0_d)*cs
yr = (y_d-y0_d)*ss - (x_d-x0_d)*cs

call srectf_real8(alp, xr, yr, depth_d, -xlength_d/2.0, xlength_d/2.0, &
            -xwidth_d/2.0, xwidth_d/2.0, sd, cd, disl1, disl2, disl3,  &
            u1_d, u2_d, u3_d, u11, u12, u21, u22, u31, u32)

tmp1 = u1_d*ss - u2_d*cs
tmp2 = u1_d*cs + u2_d*ss
      
u1 = REAL(tmp1,fp_kind)
u2 = REAL(tmp2,fp_kind)
u3 = REAL(u3_d,fp_kind)

end subroutine okada1985_real8




attributes(device) SUBROUTINE  SPOINT(ALP,X,Y,DEP,SD,CD,POT1,POT2,POT3,U1,U2,U3,U11,U12,U21,U22,U31,U32)
!*******************************************************************************
!*****                                                                     *****
!*****    SURFACE DISPLACEMENT,STRAIN,TILT                                 *****
!*****    DUE TO BURIED POINT SOURCE IN A SEMIINFINITE MEDIUM              *****
!*****                         CODED BY  Y.OKADA ... JAN 1985              *****
!*****                                                                     *****
!***** INPUT                                                               *****
!*****   ALP   : MEDIUM CONSTANT  MYU/(LAMDA+MYU)=1./((VP/VS)**2-1)        *****
!*****   X,Y   : COORDINATE OF STATION                                     *****
!*****   DEP   : SOURCE DEPTH                                              *****
!*****   SD,CD : SIN,COS OF DIP-ANGLE                                      *****
!*****          (CD=0.0, SD=+/-1.0 SHOULD BE GIVEN FOR VERTICAL FAULT)   *****
!*****   POT1,POT2,POT3 : STRIKE-, DIP- AND TENSILE-POTENCY                *****
!*****       POTENCY=(  MOMENT OF DOUBLE-COUPLE  )/MYU    FOR POT1,2       *****
!*****       POTENCY=(INTENSITY OF ISOTROPIC PART)/LAMDA  FOR POT3         *****
!                                                                          *****
!***** OUTPUT                                                              *****
!*****   U1, U2, U3      : DISPLACEMENT ( UNIT=(UNIT OF POTENCY) /         *****
!*****                   :                     (UNIT OF X,Y,D)**2  )       *****
!*****   U11,U12,U21,U22 : STRAIN       ( UNIT= UNIT OF POTENCY) /         *****
!*****   U31,U32         : TILT                (UNIT OF X,Y,D)**3  )       *****
!******************************************************************************* 
IMPLICIT NONE
REAL(fp_kind)   ::  ALP, X, Y, DEP, SD, CD, POT1, POT2, POT3, U1, U2, U3, U11, U12, U21, U22, U31, U32
REAL(fp_kind)   ::  D, P, Q, S, X2, Y2, XY, D2, R2, R, R3, R5, QR, XR, YR, XYR, DR, RD, R12, R32, R33, R53, R54
REAL(fp_kind)   ::  A1, A2, A3, A4, A5, B1, B2, B3, B4, C1, C2, C3
REAL(fp_kind)   ::  UN, QRX, FX, SDCD, QRP, FS, SDSD, QRQ, FQ
REAL(fp_kind), PARAMETER    ::  F0 = 0.0, F1 = 1.0, F2 = 2.0, F3 = 3.0, F4 = 4.0, F5 = 5.0, F8 = 8.0, F9 = 9.0
REAL(fp_kind), PARAMETER    ::  PI2 = 6.283185

D = DEP
P = Y*CD + D*SD
Q = Y*SD - D*CD
S = P*SD + Q*CD
X2 = X*X
Y2 = Y*Y
XY = X*Y
D2 = D*D
R2 = X2 + Y2 + D2
R = SQRT(R2)
R3 = R*R2
R5 = R3*R2
QR = F3*Q/R5
XR = F5*X2/R2
YR = F5*Y2/R2
XYR = F5*XY/R2
DR = F5*D/R2
RD = R + D
R12 = F1/(R*RD*RD)
R32 = R12*(F2*R + D)/R2
R33 = R12*(F3*R + D)/(R2*RD)
R53 = R12*(F8*R2 + F9*R*D + F3*D2)/(R2*R2*RD)
R54 = R12*(F5*R2 + F4*R*D +    D2)/R3*R12
A1 = ALP*Y*(R12-X2*R33)
A2 = ALP*X*(R12-Y2*R33)
A3 = ALP*X/R3 - A2
A4 = -ALP*XY*R32
A5 = ALP*( F1/(R*RD) - X2*R32)
B1 = ALP*(-F3*XY*R33      + F3*X2*XY*R54)
B2 = ALP*( F1/R3 - F3*R12 + F3*X2*Y2*R54)
B3 = ALP*( F1/R3 - F3*X2/R5) - B2
B4 = -ALP*F3*XY/R5 - B1
C1 = -ALP*Y*(R32 - X2*R53)
C2 = -ALP*X*(R32 - Y2*R53)
C3 = -ALP*F3*X*D/R5 - C2                                                                  
U1 = F0
U2 = F0
U3 = F0
U11 = F0
U12 = F0
U21 = F0
U22 = F0
U31 = F0
U32 = F0

!======================================
!====  STRIKE-SLIP CONTRIBUTION    ====
!======================================
IF(POT1.NE.F0) THEN
    UN = POT1/PI2
    QRX = QR*X
    FX = F3*X/R5*SD
    U1 = U1 - UN*( QRX*X + A1*SD)
    U2 = U2 - UN*( QRX*Y + A2*SD)
    U3 = U3 - UN*( QRX*D + A4*SD)
    U11 = U11 - UN*( QRX* (F2-XR)        + B1*SD)
    U12 = U12 - UN*(-QRX*XYR      + FX*X + B2*SD)
    U21 = U21 - UN*( QR*Y*(F1-XR)        + B2*SD)
    U22 = U22 - UN*( QRX *(F1-YR) + FX*Y + B4*SD)
    U31 = U31 - UN*( QR*D*(F1-XR)        + C1*SD)
    U32 = U32 - UN*(-QRX*DR*Y     + FX*D + C2*SD)
ENDIF

!======================================
!=====    DIP-SLIP CONTRIBUTION   =====
!======================================
IF(POT2.NE.F0) THEN
    UN = POT2/PI2
    SDCD = SD*CD
    QRP = QR*P
    FS = F3*S/R5
    U1 = U1 - UN*( QRP*X - A3*SDCD)
    U2 = U2 - UN*( QRP*Y - A1*SDCD)
    U3 = U3 - UN*( QRP*D - A5*SDCD)
    U11 = U11 - UN*( QRP*(F1-XR)        - B3*SDCD)
    U12 = U12 - UN*(-QRP*XYR     + FS*X - B1*SDCD)
    U21 = U21 - UN*(-QRP*XYR            - B1*SDCD)
    U22 = U22 - UN*( QRP*(F1-YR) + FS*Y - B2*SDCD)
    U31 = U31 - UN*(-QRP*DR*X           - C3*SDCD)
    U32 = U32 - UN*(-QRP*DR*Y    + FS*D - C1*SDCD)
ENDIF

!========================================
!=====  TENSILE-FAULT CONTRIBUTION  =====
!========================================
IF(POT3.NE.F0) THEN
    UN = POT3/PI2
    SDSD = SD*SD
    QRQ = QR*Q
    FQ = F2*QR*SD
    U1 = U1 + UN*( QRQ*X - A3*SDSD)
    U2 = U2 + UN*( QRQ*Y - A1*SDSD)
    U3 = U3 + UN*( QRQ*D - A5*SDSD)
    U11 = U11+ UN*( QRQ*(F1-XR)        - B3*SDSD)
    U12 = U12+ UN*(-QRQ*XYR     + FQ*X - B1*SDSD)
    U21 = U21+ UN*(-QRQ*XYR            - B1*SDSD)
    U22 = U22+ UN*( QRQ*(F1-YR) + FQ*Y - B2*SDSD)
    U31 = U31+ UN*(-QRQ*DR*X           - C3*SDSD)
    U32 = U32+ UN*(-QRQ*DR*Y    + FQ*D - C1*SDSD)
ENDIF

END SUBROUTINE SPOINT



attributes(device) SUBROUTINE  SRECTF(ALP,X,Y,DEP,AL1,AL2,AW1,AW2,SD,CD,DISL1,DISL2,DISL3,U1,U2,U3,U11,U12,U21,U22,U31,U32)
!*******************************************************************************
!*****                                                                     *****
!*****    SURFACE DISPLACEMENT,STRAIN,TILT                                 *****
!*****    DUE TO RECTANGULAR FAULT IN A HALF-SPACE                         *****
!*****              CODED BY  Y.OKADA ... JAN 1985                         *****
!*****                                                                     *****
!***** INPUT                                                               *****
!*****   ALP     : MEDIUM CONSTANT  MYU/(LAMDA+MYU)=1./((VP/VS)**2-1)      *****
!*****   X,Y     : COORDINATE OF STATION                                   *****
!*****   DEP     : SOURCE DEPTH                                            *****
!*****   AL1,AL2 : FAULT LENGTH RANGE                                      *****
!*****   AW1,AW2 : FAULT WIDTH RANGE                                       *****
!*****   SD,CD   : SIN,COS OF DIP-ANGLE                                    *****
!*****          (CD=0.0, SD=+/-1.0 SHOULD BE GIVEN FOR VERTICAL FAULT)   *****
!*****   DISL1,DISL2,DISL3 : STRIKE-, DIP- AND TENSILE-DISLOCATION         ***** 
!*****                                                                     *****
!***** OUTPUT                                                              *****
!*****   U1, U2, U3      : DISPLACEMENT ( UNIT= UNIT OF DISL     )         *****
!*****   U11,U12,U21,U22 : STRAIN       ( UNIT= UNIT OF DISL /             *****
!*****   U31,U32         : TILT                 UNIT OF X,Y,,,AW )         *****
!*****                                                                     *****
!***** SUBROUTINE USED...SRECTG                                            *****
!*******************************************************************************   
IMPLICIT NONE
REAL(fp_kind)   ::  ALP, X, Y, DEP, AL1, AL2, AW1, AW2, SD, CD, DISL1, DISL2, DISL3
REAL(fp_kind)   ::  U1, U2, U3, U11, U12, U21, U22, U31, U32
REAL(fp_kind)   ::  P, Q, ET, XI, SIGN
INTEGER ::  J, K, JK
REAL(fp_kind), DIMENSION(9) ::  U, DU
REAL(fp_kind), PARAMETER    ::  F0 = 0.0, F1 = 1.0

P = Y*CD + DEP*SD
Q = Y*SD - DEP*CD
U = F0

DO K = 1,2
    IF(K.EQ.1)  ET = P - AW1
    IF(K.EQ.2)  ET = P - AW2
    DO J = 1,2
        IF(J.EQ.1)  XI = X - AL1
        IF(J.EQ.2)  XI = X - AL2
        JK = J + K
        IF(JK.EQ.3) THEN
            SIGN = -F1
        ELSE
            SIGN = F1
        ENDIF
        CALL SRECTG(ALP,XI,ET,Q,SD,CD,DISL1,DISL2,DISL3,DU(1),DU(2),DU(3),DU(4),DU(5),DU(6),DU(7),DU(8),DU(9))
        U = U + SIGN*DU
    ENDDO !loop for J
ENDDO !loop for K

U1 = U(1)
U2 = U(2)
U3 = U(3)
U11 = U(4)
U12 = U(5)
U21 = U(6)
U22 = U(7)
U31 = U(8)
U32 = U(9)

END SUBROUTINE SRECTF




attributes(device) SUBROUTINE  SRECTF_REAL8(ALP,X,Y,DEP,AL1,AL2,AW1,AW2,SD,CD,DISL1,DISL2,DISL3,U1,U2,U3,U11,U12,U21,U22,U31,U32)
!*******************************************************************************
!*****                                                                     *****
!*****    SURFACE DISPLACEMENT,STRAIN,TILT                                 *****
!*****    DUE TO RECTANGULAR FAULT IN A HALF-SPACE                         *****
!*****              CODED BY  Y.OKADA ... JAN 1985                         *****
!*****                                                                     *****
!***** INPUT                                                               *****
!*****   ALP     : MEDIUM CONSTANT  MYU/(LAMDA+MYU)=1./((VP/VS)**2-1)      *****
!*****   X,Y     : COORDINATE OF STATION                                   *****
!*****   DEP     : SOURCE DEPTH                                            *****
!*****   AL1,AL2 : FAULT LENGTH RANGE                                      *****
!*****   AW1,AW2 : FAULT WIDTH RANGE                                       *****
!*****   SD,CD   : SIN,COS OF DIP-ANGLE                                    *****
!*****          (CD=0.0, SD=+/-1.0 SHOULD BE GIVEN FOR VERTICAL FAULT)   *****
!*****   DISL1,DISL2,DISL3 : STRIKE-, DIP- AND TENSILE-DISLOCATION         ***** 
!*****                                                                     *****
!***** OUTPUT                                                              *****
!*****   U1, U2, U3      : DISPLACEMENT ( UNIT= UNIT OF DISL     )         *****
!*****   U11,U12,U21,U22 : STRAIN       ( UNIT= UNIT OF DISL /             *****
!*****   U31,U32         : TILT                 UNIT OF X,Y,,,AW )         *****
!*****                                                                     *****
!***** SUBROUTINE USED...SRECTG                                            *****
!*******************************************************************************   
IMPLICIT NONE
REAL*8  ::  ALP, X, Y, DEP, AL1, AL2, AW1, AW2, SD, CD, DISL1, DISL2, DISL3
REAL*8  ::  U1, U2, U3, U11, U12, U21, U22, U31, U32
REAL*8  ::  P, Q, ET, XI, SIGN
INTEGER ::  J, K, JK
REAL*8, DIMENSION(9)    ::  U, DU
REAL*8, PARAMETER       ::  F0 = 0.0, F1 = 1.0

P = Y*CD + DEP*SD
Q = Y*SD - DEP*CD
U = F0

DO K = 1,2
    IF(K.EQ.1)  ET = P - AW1
    IF(K.EQ.2)  ET = P - AW2
    DO J = 1,2
        IF(J.EQ.1)  XI = X - AL1
        IF(J.EQ.2)  XI = X - AL2
        JK = J + K
        IF(JK.EQ.3) THEN
            SIGN = -F1
        ELSE
            SIGN = F1
        ENDIF
        CALL SRECTG_REAL8(ALP,XI,ET,Q,SD,CD,DISL1,DISL2,DISL3,DU(1),DU(2),DU(3),DU(4),DU(5),DU(6),DU(7),DU(8),DU(9))
        U = U + SIGN*DU
    ENDDO !loop for J
ENDDO !loop for K

U1 = U(1)
U2 = U(2)
U3 = U(3)
U11 = U(4)
U12 = U(5)
U21 = U(6)
U22 = U(7)
U31 = U(8)
U32 = U(9)

END SUBROUTINE SRECTF_REAL8 




attributes(device) SUBROUTINE  SRECTG(ALP,XI,ET,Q,SD,CD,DISL1,DISL2,DISL3,U1,U2,U3,U11,U12,U21,U22,U31,U32)
!*******************************************************************************
!*****                                                                     *****
!*****  INDEFINITE INTEGRAL OF SURFACE DISPLACEMENT,STRAIN,TILT            *****
!*****  DUE TO RECTANGULAR FAULT IN A HALF-SPACE                           *****
!*****                          CODED BY  Y.OKADA ... JAN 1985             *****
!*****                                                                     *****
!***** INPUT                                                               *****
!*****   ALP     : MEDIUM CONSTANT  MYU/(LAMDA+MYU)=1./((VP/VS)**2-1)      *****
!*****   XI,ET,Q : FAULT COORDINATE                                        *****
!*****   SD,CD   : SIN,COS OF DIP-ANGLE                                    *****
!*****          (CD=0.D0, SD=+/-1.D0 SHOULD BE GIVEN FOR VERTICAL FAULT)   *****
!*****   DISL1,DISL2,DISL3 : STRIKE-, DIP- AND TENSILE-DISLOCATION         *****
!*****                                                                     *****
!***** OUTPUT                                                              *****
!*****   U1, U2, U3      : DISPLACEMENT ( UNIT= UNIT OF DISL    )          *****
!*****   U11,U12,U21,U22 : STRAIN       ( UNIT= UNIT OF DISL /             *****
!*****   U31,U32         : TILT                 UNIT OF XI,ET,Q )          *****
!*******************************************************************************
IMPLICIT NONE
REAL(fp_kind)   ::  ALP,XI,ET,Q,SD,CD,DISL1,DISL2,DISL3,U1,U2,U3,U11,U12,U21,U22,U31,U32
REAL(fp_kind)   ::  XI2, ET2, Q2, R2, R, R3, D, X, Y, RET, RE, RD, RRD, RRX, RRE, RD2
REAL(fp_kind)   ::  TT, TD, DLE, AXI, AET, A1, A2, A3, A4, A5, B1, B2, B3, B4, C1, C2, C3
REAL(fp_kind)   ::  UN, REQ, SDCD, SDSD 
REAL(fp_kind), PARAMETER    ::  F0 = 0.0, F1 = 1.0, F2 = 2.0, PI2 = 6.283185

XI2 = XI*XI
ET2 = ET*ET
Q2 = Q*Q
R2 = XI2 + ET2 + Q2
R = SQRT(R2)
R3 = R*R2
D = ET*SD - Q*CD
Y = ET*CD + Q*SD
RET = R + ET
IF(RET.LT.F0)  RET = F0
RD = R + D
RRD = F1/(R*RD)

IF(Q.NE.F0) THEN
    TT = ATAN(XI*ET/(Q*R))
ELSE
    TT = F0
ENDIF
IF(RET.NE.F0) THEN
    RE = F1/RET
    DLE = LOG(RET)
ELSE
    RE = F0
    DLE = -LOG(R-ET)
ENDIF
RRX = F1/(R*(R+XI))
RRE = RE/R
AXI = (F2*R+XI)*RRX*RRX/R
AET = (F2*R+ET)*RRE*RRE/R

IF(CD.NE.F0) THEN
!==============================
!=====   INCLINED FAULT   =====
!==============================
    TD = SD/CD
    X = SQRT(XI2+Q2)
    IF(XI.NE.F0) THEN
        A5 = ALP*F2/CD*ATAN((ET*(X+Q*CD)+X*(R+X)*SD)/(XI*(R+X)*CD))
    ELSE
        A5 = F0
    ENDIF
    A4 = ALP/CD*(LOG(RD) - SD*DLE)
    A3 = ALP*(Y/RD/CD-DLE) + TD*A4
    A1 =-ALP/CD*XI/RD      - TD*A5
    C1 = ALP/CD*XI*(RRD - SD*RRE)
    C3 = ALP/CD*(Q*RRE - Y*RRD)
    B1 = ALP/CD*(XI2*RRD - F1)/RD - TD*C3
    B2 = ALP/CD*XI*Y*RRD/RD       - TD*C1
ELSE
!==============================
!=====   VERTICAL FAULT   =====
!==============================
    RD2 = RD*RD
    A1 =-ALP/F2*XI*Q/RD2
    A3 = ALP/F2*(ET/RD + Y*Q/RD2 - DLE)
    A4 =-ALP*Q/RD
    A5 =-ALP*XI*SD/RD
    B1 = ALP/F2*  Q  /RD2*(F2*XI2*RRD - F1)
    B2 = ALP/F2*XI*SD/RD2*(F2*Q2 *RRD - F1)
    C1 = ALP*XI*Q*RRD/RD
    C3 = ALP*SD/RD*(XI2*RRD - F1)
ENDIF

A2 = -ALP*DLE - A3
B3 = -ALP*XI*RRE - B2
B4 = -ALP*( CD/R + Q*SD*RRE) - B1
C2 =  ALP*(-SD/R + Q*CD*RRE) - C3
U1 = F0
U2 = F0
U3 = F0
U11 = F0
U12 = F0
U21 = F0
U22 = F0
U31 = F0
U32 = F0

!======================================
!=====  STRIKE-SLIP CONTRIBUTION  =====
!======================================
IF(DISL1.NE.F0) THEN
    UN = DISL1/PI2
    REQ = RRE*Q
    U1 = U1 - UN*(REQ*XI +   TT    + A1*SD)
    U2 = U2 - UN*(REQ*Y  + Q*CD*RE + A2*SD)
    U3 = U3 - UN*(REQ*D  + Q*SD*RE + A4*SD)
    U11 = U11 + UN*( XI2*Q*AET - B1*SD )
    U12 = U12 + UN*( XI2*XI*( D/(ET2+Q2)/R3 - AET*SD ) - B2*SD)
    U21 = U21 + UN*( XI*Q/R3*CD + (XI*Q2*AET - B2)*SD)
    U22 = U22 + UN*( Y *Q/R3*CD + (Q*SD*(Q2*AET-F2*RRE)-(XI2+ET2)/R3*CD - B4)*SD)
    U31 = U31 + UN*(-XI*Q2*AET*CD + (XI*Q/R3 - C1)*SD)
    U32 = U32 + UN*( D*Q/R3*CD + (XI2*Q*AET*CD - SD/R + Y*Q/R3 - C2)*SD)
ENDIF

!===================================
!=====  DIP-SLIP CONTRIBUTION  =====
!===================================
IF(DISL2.NE.F0) THEN
    UN = DISL2/PI2
    SDCD = SD*CD
    U1 = U1 - UN*(Q/R             - A3*SDCD)
    U2 = U2 - UN*(Y*Q*RRX + CD*TT - A1*SDCD)
    U3 = U3 - UN*(D*Q*RRX + SD*TT - A5*SDCD)
    U11 = U11 + UN*(XI*Q/R3            + B3*SDCD)
    U12 = U12 + UN*(Y *Q/R3 - SD/R     + B1*SDCD)
    U21 = U21 + UN*(Y *Q/R3 + Q*CD*RRE + B1*SDCD)
    U22 = U22 + UN*(Y*Y*Q*AXI - (F2*Y*RRX + XI*CD*RRE)*SD + B2*SDCD)
    U31 = U31 + UN*(D *Q/R3 + Q*SD*RRE + C3*SDCD)
    U32 = U32 + UN*(Y*D*Q*AXI - (F2*D*RRX + XI*SD*RRE)*SD + C1*SDCD)
ENDIF

!========================================
!=====  TENSILE-FAULT CONTRIBUTION  =====
!========================================
IF(DISL3.NE.F0) THEN
    UN = DISL3/PI2
    SDSD = SD*SD
    U1 = U1 + UN*(Q2*RRE                       - A3*SDSD)
    U2 = U2 + UN*(-D*Q*RRX - SD*(XI*Q*RRE - TT) - A1*SDSD)
    U3 = U3 + UN*( Y*Q*RRX + CD*(XI*Q*RRE - TT) - A5*SDSD)
    U11 = U11 - UN*( XI*Q2*AET             + B3*SDSD)
    U12 = U12 - UN*(-D*Q/R3 - XI2*Q*AET*SD + B1*SDSD)
    U21 = U21 - UN*( Q2*(CD/R3 + Q*AET*SD) + B1*SDSD)
    U22 = U22 - UN*((Y*CD-D*SD)*Q2*AXI - F2*Q*SD*CD*RRX - (XI*Q2*AET - B2)*SDSD)
    U31 = U31 - UN*( Q2*(SD/R3 - Q*AET*CD) + C3*SDSD)
    U32 = U32 - UN*((Y*SD+D*CD)*Q2*AXI + XI*Q2*AET*SD*CD - (F2*Q*RRX - C1)*SDSD)
ENDIF

END SUBROUTINE SRECTG




attributes(device) SUBROUTINE  SRECTG_REAL8(ALP,XI,ET,Q,SD,CD,DISL1,DISL2,DISL3,U1,U2,U3,U11,U12,U21,U22,U31,U32)
!*******************************************************************************
!*****                                                                     *****
!*****  INDEFINITE INTEGRAL OF SURFACE DISPLACEMENT,STRAIN,TILT            *****
!*****  DUE TO RECTANGULAR FAULT IN A HALF-SPACE                           *****
!*****                          CODED BY  Y.OKADA ... JAN 1985             *****
!*****                                                                     *****
!***** INPUT                                                               *****
!*****   ALP     : MEDIUM CONSTANT  MYU/(LAMDA+MYU)=1./((VP/VS)**2-1)      *****
!*****   XI,ET,Q : FAULT COORDINATE                                        *****
!*****   SD,CD   : SIN,COS OF DIP-ANGLE                                    *****
!*****          (CD=0.D0, SD=+/-1.D0 SHOULD BE GIVEN FOR VERTICAL FAULT)   *****
!*****   DISL1,DISL2,DISL3 : STRIKE-, DIP- AND TENSILE-DISLOCATION         *****
!*****                                                                     *****
!***** OUTPUT                                                              *****
!*****   U1, U2, U3      : DISPLACEMENT ( UNIT= UNIT OF DISL    )          *****
!*****   U11,U12,U21,U22 : STRAIN       ( UNIT= UNIT OF DISL /             *****
!*****   U31,U32         : TILT                 UNIT OF XI,ET,Q )          *****
!*******************************************************************************
IMPLICIT NONE
REAL*8  ::  ALP,XI,ET,Q,SD,CD,DISL1,DISL2,DISL3,U1,U2,U3,U11,U12,U21,U22,U31,U32
REAL*8  ::  XI2, ET2, Q2, R2, R, R3, D, X, Y, RET, RE, RD, RRD, RRX, RRE, RD2
REAL*8  ::  TT, TD, DLE, AXI, AET, A1, A2, A3, A4, A5, B1, B2, B3, B4, C1, C2, C3
REAL*8  ::  UN, REQ, SDCD, SDSD 
REAL*8, PARAMETER   ::  F0 = 0.0, F1 = 1.0, F2 = 2.0, PI2 = 6.283185

XI2 = XI*XI
ET2 = ET*ET
Q2 = Q*Q
R2 = XI2 + ET2 + Q2
R = SQRT(R2)
R3 = R*R2
D = ET*SD - Q*CD
Y = ET*CD + Q*SD
RET = R + ET
IF(RET.LT.F0)  RET = F0
RD = R + D
RRD = F1/(R*RD)

IF(Q.NE.F0) THEN
    TT = ATAN(XI*ET/(Q*R))
ELSE
    TT = F0
ENDIF
IF(RET.NE.F0) THEN
    RE = F1/RET
    DLE = LOG(RET)
ELSE
    RE = F0
    DLE = -LOG(R-ET)
ENDIF
RRX = F1/(R*(R+XI))
RRE = RE/R
AXI = (F2*R+XI)*RRX*RRX/R
AET = (F2*R+ET)*RRE*RRE/R

IF(CD.NE.F0) THEN
!==============================
!=====   INCLINED FAULT   =====
!==============================
    TD = SD/CD
    X = SQRT(XI2+Q2)
    IF(XI.NE.F0) THEN
        A5 = ALP*F2/CD*ATAN((ET*(X+Q*CD)+X*(R+X)*SD)/(XI*(R+X)*CD))
    ELSE
        A5 = F0
    ENDIF
    A4 = ALP/CD*(LOG(RD) - SD*DLE)
    A3 = ALP*(Y/RD/CD-DLE) + TD*A4
    A1 =-ALP/CD*XI/RD      - TD*A5
    C1 = ALP/CD*XI*(RRD - SD*RRE)
    C3 = ALP/CD*(Q*RRE - Y*RRD)
    B1 = ALP/CD*(XI2*RRD - F1)/RD - TD*C3
    B2 = ALP/CD*XI*Y*RRD/RD       - TD*C1
ELSE
!==============================
!=====   VERTICAL FAULT   =====
!==============================
    RD2 = RD*RD
    A1 =-ALP/F2*XI*Q/RD2
    A3 = ALP/F2*(ET/RD + Y*Q/RD2 - DLE)
    A4 =-ALP*Q/RD
    A5 =-ALP*XI*SD/RD
    B1 = ALP/F2*  Q  /RD2*(F2*XI2*RRD - F1)
    B2 = ALP/F2*XI*SD/RD2*(F2*Q2 *RRD - F1)
    C1 = ALP*XI*Q*RRD/RD
    C3 = ALP*SD/RD*(XI2*RRD - F1)
ENDIF

A2 = -ALP*DLE - A3
B3 = -ALP*XI*RRE - B2
B4 = -ALP*( CD/R + Q*SD*RRE) - B1
C2 =  ALP*(-SD/R + Q*CD*RRE) - C3
U1 = F0
U2 = F0
U3 = F0
U11 = F0
U12 = F0
U21 = F0
U22 = F0
U31 = F0
U32 = F0

!======================================
!=====  STRIKE-SLIP CONTRIBUTION  =====
!======================================
IF(DISL1.NE.F0) THEN
    UN = DISL1/PI2
    REQ = RRE*Q
    U1 = U1 - UN*(REQ*XI +   TT    + A1*SD)
    U2 = U2 - UN*(REQ*Y  + Q*CD*RE + A2*SD)
    U3 = U3 - UN*(REQ*D  + Q*SD*RE + A4*SD)
    U11 = U11 + UN*( XI2*Q*AET - B1*SD )
    U12 = U12 + UN*( XI2*XI*( D/(ET2+Q2)/R3 - AET*SD ) - B2*SD)
    U21 = U21 + UN*( XI*Q/R3*CD + (XI*Q2*AET - B2)*SD)
    U22 = U22 + UN*( Y *Q/R3*CD + (Q*SD*(Q2*AET-F2*RRE)-(XI2+ET2)/R3*CD - B4)*SD)
    U31 = U31 + UN*(-XI*Q2*AET*CD + (XI*Q/R3 - C1)*SD)
    U32 = U32 + UN*( D*Q/R3*CD + (XI2*Q*AET*CD - SD/R + Y*Q/R3 - C2)*SD)
ENDIF

!===================================
!=====  DIP-SLIP CONTRIBUTION  =====
!===================================
IF(DISL2.NE.F0) THEN
    UN = DISL2/PI2
    SDCD = SD*CD
    U1 = U1 - UN*(Q/R             - A3*SDCD)
    U2 = U2 - UN*(Y*Q*RRX + CD*TT - A1*SDCD)
    U3 = U3 - UN*(D*Q*RRX + SD*TT - A5*SDCD)
    U11 = U11 + UN*(XI*Q/R3            + B3*SDCD)
    U12 = U12 + UN*(Y *Q/R3 - SD/R     + B1*SDCD)
    U21 = U21 + UN*(Y *Q/R3 + Q*CD*RRE + B1*SDCD)
    U22 = U22 + UN*(Y*Y*Q*AXI - (F2*Y*RRX + XI*CD*RRE)*SD + B2*SDCD)
    U31 = U31 + UN*(D *Q/R3 + Q*SD*RRE + C3*SDCD)
    U32 = U32 + UN*(Y*D*Q*AXI - (F2*D*RRX + XI*SD*RRE)*SD + C1*SDCD)
ENDIF

!========================================
!=====  TENSILE-FAULT CONTRIBUTION  =====
!========================================
IF(DISL3.NE.F0) THEN
    UN = DISL3/PI2
    SDSD = SD*SD
    U1 = U1 + UN*(Q2*RRE                       - A3*SDSD)
    U2 = U2 + UN*(-D*Q*RRX - SD*(XI*Q*RRE - TT) - A1*SDSD)
    U3 = U3 + UN*( Y*Q*RRX + CD*(XI*Q*RRE - TT) - A5*SDSD)
    U11 = U11 - UN*( XI*Q2*AET             + B3*SDSD)
    U12 = U12 - UN*(-D*Q/R3 - XI2*Q*AET*SD + B1*SDSD)
    U21 = U21 - UN*( Q2*(CD/R3 + Q*AET*SD) + B1*SDSD)
    U22 = U22 - UN*((Y*CD-D*SD)*Q2*AXI - F2*Q*SD*CD*RRX - (XI*Q2*AET - B2)*SDSD)
    U31 = U31 - UN*( Q2*(SD/R3 - Q*AET*CD) + C3*SDSD)
    U32 = U32 - UN*((Y*SD+D*CD)*Q2*AXI + XI*Q2*AET*SD*CD - (F2*Q*RRX - C1)*SDSD)
ENDIF

END SUBROUTINE SRECTG_REAL8


end module okada_device